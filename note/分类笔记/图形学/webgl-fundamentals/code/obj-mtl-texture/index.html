<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="UTF-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0" />
    <title>Obj模型加载-MTL-TEXTURE</title>
    <style>
      body {
        margin: 0;
      }

      canvas {
        width: 100vw;
        height: 100vh;
        display: block;
      }
    </style>
  </head>

  <body>
    <canvas id="canvas"></canvas>
    <script id="vertex-shader-3d" type="x-shader/x-vertex">
      attribute vec4 a_position;
      attribute vec3 a_normal;
      attribute vec3 a_tangent;
      attribute vec2 a_texcoord;
      attribute vec4 a_color;

      uniform mat4 u_projection;
      uniform mat4 u_view;
      uniform mat4 u_world;
      uniform vec3 u_viewWorldPosition;

      varying vec3 v_normal;
      varying vec3 v_tangent;
      varying vec3 v_surfaceToView;
      varying vec2 v_texcoord;
      varying vec4 v_color;

      void main() {
          vec4 worldPosition = u_world * a_position;
          gl_Position = u_projection * u_view * worldPosition;
          v_surfaceToView = u_viewWorldPosition - worldPosition.xyz;

          // 凹凸纹理设置
          mat3 normalMat = mat3(u_world);
          v_normal = normalize(normalMat * a_normal);
          v_tangent = normalize(normalMat * a_tangent);

          v_texcoord = a_texcoord;
          v_color = a_color;
      }
    </script>
    <script id="fragment-shader-3d" type="x-shader/x-fragment">
      precision highp float;

      varying vec3 v_normal;
      varying vec3 v_surfaceToView;
      varying vec3 v_tangent;
      varying vec2 v_texcoord;
      varying vec4 v_color;

      uniform vec3 diffuse;
      uniform sampler2D diffuseMap;
      uniform vec3 ambient;
      uniform vec3 emissive;
      uniform vec3 specular;
      uniform sampler2D specularMap;
      uniform float shininess;
      uniform sampler2D normalMap;
      uniform float opacity;
      uniform vec3 u_lightDirection;
      uniform vec3 u_ambientLight;

      void main() {
          vec3 normal = normalize(v_normal);
          vec3 tangent = normalize(v_tangent);
          vec3 bitangent = normalize(cross(normal, tangent));

          mat3 tbn = mat3(tangent, bitangent, normal);
          normal = texture2D(normalMap, v_texcoord).rgb * 2. - 1.;
          normal = normalize(tbn * normal);

          vec3 surfaceToViewDirection = normalize(v_surfaceToView);
          vec3 halfVector = normalize(u_lightDirection + surfaceToViewDirection);

          float fakeLight = dot(u_lightDirection, normal) * .5 + .5;
          float specularLight = clamp(dot(normal, halfVector), 0.0, 1.0);
          vec4 specularMapColor = texture2D(specularMap, v_texcoord);
          vec3 effectiveSpecular = specular * specularMapColor.rgb;


          vec4 diffuseMapColor = texture2D(diffuseMap, v_texcoord);
          vec3 effectiveDiffuse = diffuse * diffuseMapColor.rgb * v_color.rgb;
          float effectiveOpacity = opacity * diffuseMapColor.a * v_color.a;

          gl_FragColor = vec4(
              emissive +
              ambient * u_ambientLight +
              effectiveDiffuse * fakeLight +
              effectiveSpecular * pow(specularLight, shininess),
              effectiveOpacity);
      }
    </script>

    <script src="../lib/webgl-utils.js"></script>
    <script src="../lib/m4.js"></script>

    <script src="./index.js"></script>
  </body>
</html>
