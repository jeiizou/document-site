<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="UTF-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0" />
    <title>光照: 点光源</title>
    <style>
      body {
        margin: 0;
      }

      canvas {
        width: 100vw;
        height: 100vh;
        display: block;
      }
    </style>
  </head>

  <body>
    <canvas id="canvas"></canvas>
    <script id="vertex-shader-3d" type="x-shader/x-vertex">
      // 顶点坐标
      attribute vec4 a_position;
      // 法向量
      attribute vec3 a_normal;
      // 表面到光源的方向向量
      uniform vec3 u_lightWorldPosition;
      // 表面到相机的方向向量
      uniform vec3 u_viewWorldPosition;
      // 世界坐标
      uniform mat4 u_world;
      // 世界视角 投影矩阵x
      uniform mat4 u_worldViewProjection;
      uniform mat4 u_worldInverseTranspose;

      // 法向量数据
      varying vec3 v_normal;
      // 指向光源的表面向量
      varying vec3 v_surfaceToLight;
      // 指向相机的表面向量
      varying vec3 v_surfaceToView;

      void main(){
          // 将顶点像素坐标映射到裁剪空间
          gl_Position = u_worldViewProjection * a_position;

          // 重定向法向量并传递给片元着色器
          v_normal = mat3(u_worldInverseTranspose) * a_normal;

          // 计算表面的世界坐标
          vec3 surfaceWorldPosition = (u_world * a_position).xyz;

          // 计算表面到光源的方向
          // 传递给片元着色器
          v_surfaceToLight = u_lightWorldPosition - surfaceWorldPosition;

          // 计算表面到相机的方向
          // 然后传递到片断着色器
          v_surfaceToView = u_viewWorldPosition - surfaceWorldPosition;
      }
    </script>
    <script id="fragment-shader-3d" type="x-shader/x-fragment">
      precision mediump float;

      varying vec3 v_normal;
      varying vec3 v_surfaceToLight;
      varying vec3 v_surfaceToView;

      uniform vec4 u_color;
      uniform float u_shininess; // 高光的亮度
      // 聚光灯
      uniform vec3 u_lightDirection; // 聚光灯的方向
      // 聚光灯的范围限定, 用两个变量实现比较柔和的效果
      uniform float u_innerLimit;     // 在点乘空间中
      uniform float u_outerLimit;     // 在点乘空间中

      void main() {
          // 归一化的法向量
          vec3 normal = normalize(v_normal);
          // 从表面指向点光源的向量
          vec3 surfaceToLightDirection = normalize(v_surfaceToLight);

          vec3 surfaceToViewDirection = normalize(v_surfaceToView);
          vec3 halfVector = normalize(surfaceToLightDirection + surfaceToViewDirection);

          float dotFromDirection = dot(surfaceToLightDirection,
                                          -u_lightDirection);
          // float limitRange = u_innerLimit - u_outerLimit;
          // float inLight = clamp((dotFromDirection - u_outerLimit) / limitRange, 0.0, 1.0);
          float inLight = smoothstep(u_outerLimit, u_innerLimit, dotFromDirection);
          // 法向量和光源向量的点积, 表示两个向量之间的余弦值
          float light = inLight * dot(normal, surfaceToLightDirection);
           // 法向量和 相机光线向量的点积, 用于计算镜面反光
          float specular = inLight * pow(dot(normal, halfVector), u_shininess);


          gl_FragColor = u_color;

          // 只将颜色部分（不包含 alpha） 和光照相乘
          gl_FragColor.rgb *= light ;

          // 直接加上高光
          gl_FragColor.rgb += specular ;
      }
    </script>
    <script src="../lib/webgl-utils.js"></script>
    <script src="../lib/m4.js"></script>

    <script src="./data.js"></script>
    <script src="./index.js"></script>
  </body>
</html>
